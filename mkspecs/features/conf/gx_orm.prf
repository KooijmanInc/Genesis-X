# SPDX-License-Identifier: (LicenseRef-KooijmanInc-Commercial OR GPL-3.0-only)
# Copyright (c) 2025 Kooijman Incorporate Holding B.V.

!defined(__GX_ORM_PRF_INCLUDED__, var) {
    __GX_ORM_PRF_INCLUDED__ = 1

    message([genesisx_orm] module loaded from $$PWD)

    GX_ORM_INC_ROOT = $$clean_path($$GENESISX_BUILD_ROOT/orm/include)

    INCLUDEPATH += $$GX_ORM_INC_ROOT $$clean_path($$GENESISX_BUILD_ROOT/core/include)
    QMAKE_LIBDIR += $$LIBDIRS

    GX_ABI = $$QT_ARCH
    isEmpty(GX_ABI): GX_ABI = arm64-v8a
    contains(GX_ABI, arm64) {
        GX_ABI = arm64-v8a
    } else: contains(GX_ABI, x86_64) {
        GX_ABI = x86_64
    }

    android {
        # !contains(LIBS, -lgenesisx_$${GX_ABI}) {
        LIBS += -lgenesisx_orm_$${GX_ABI}

        message([genesisx_orm] setting orm lib -lgenesisx_orm_$${GX_ABI})
        # }
    } else {
        # !contains(LIBS, -lgenesisx) {
        #     LIBS += -lgenesisx
        #     include(gx_core_sub.prf)
        #     message(here arrived $$GX_SO)
        # }
        LIBS += -lgenesisx_orm

        message([genesisx_orm] setting orm lib -lgenesisx_orm)
    }

    win32 {
        GX_DLL_ORM = $$shell_path($$LIBDIRS/genesisx_orm.dll)
        GX_DLL_ORM_DEST = $$shell_path($$DEST_LIBDIRS/genesisx_orm.dll)
        GX_A_ORM = $$shell_path($$LIBDIRS/libgenesisx_orm.a)
        GX_A_ORM_DEST = $$shell_path($$DEST_LIBDIRS/libgenesisx_orm.a)

        exists($$GX_DLL_ORM) {
            QMAKE_POST_LINK += $$escape_expand(\\n\\t)$(COPY_FILE) $$shell_quote($$GX_DLL_ORM) $$shell_quote($$GX_DLL_ORM_DEST)
            message([genesisx_orm] orm library .dll file copied to $$DEST_LIBDIRS)
        }
        exists($$GX_A_ORM) {
            QMAKE_POST_LINK += $$escape_expand(\\n\\t)$(COPY_FILE) $$shell_quote($$GX_A_ORM) $$shell_quote($$GX_A_ORM_DEST)
            message([genesisx_orm] orm library $$GX_A_ORM $$GX_ORM_A file copied to $$DEST_LIBDIRS)
        }
    }

    android {
        GX_ORM_SO = $$shell_path($$LIBDIRS/libgenesisx_orm_$${GX_ABI}.so)
        GX_ORM_SO_DEST = $$shell_path($$DEST_LIBDIRS/libgenesisx_orm_$${GX_ABI}.so)
        exists($$GX_ORM_SO) {
            QMAKE_POST_LINK += $$escape_expand(\\n\\t)$(COPY_FILE) $$shell_quote($$GX_ORM_SO) $$shell_quote($$GX_ORM_SO_DEST)
            message([genesisx_orm] library .so file copied to $$DEST_LIBDIRS)
        }

        ANDROID_EXTRA_LIBS += $$GX_ORM_SO
    }
}
