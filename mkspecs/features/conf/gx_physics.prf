# SPDX-License-Identifier: (LicenseRef-KooijmanInc-Commercial OR GPL-3.0-only)
# Copyright (c) 2025 Kooijman Incorporate Holding B.V.

!defined(__GX_PHYSICS_PRF_INCLUDED__, var) {
    __GX_PHYSICS_PRF_INCLUDED__ = 1

    message([genesisx_physics] module loaded from $$PWD)

    # ---- Set physics root path ----
    GX_PHYSICS_INC_ROOT = $$clean_path($$GENESISX_BUILD_ROOT/core/include)

    INCLUDEPATH += $$GX_PHYSICS_INC_ROOT
    QMAKE_LIBDIR += $$LIBDIRS

    GX_ABI = $$QT_ARCH
    isEmpty(GX_ABI): GX_ABI = arm64-v8a
    contains(GX_ABI, arm64): GX_ABI = arm64-v8a

    android {
        !contains(LIBS, -lgenesisx_$${GX_ABI}) {
            LIBS += -lgenesisx_$${GX_ABI}

            message([genesisx_physics] setting core lib -lgenesisx_$${GX_ABI})
        }
    } else {
        !contains(LIBS, -lgenesisx) {
            LIBS += -lgenesisx

            message([genesisx_physics] setting core lib -lgenesisx)
        }
    }

    win32 {
        GX_DLL = $$shell_path($$LIBDIRS/genesisx.dll)
        GX_DLL_DEST = $$shell_path($$DEST_LIBDIRS/genesisx.dll)

        GX_DLL_PHYSICS = $$shell_path($$LIBDIRS/genesisx_physics.dll)
        GX_DLL_PHYSICS_DEST = $$shell_path($$DEST_LIBDIRS/genesisx_physics.dll)

        exists($$GX_DLL) {
            QMAKE_POST_LINK += $$escape_expand(\\n\\t)$(COPY_FILE) $$shell_quote($$GX_DLL) $$shell_quote($$GX_DLL_DEST)
            message([genesisx_physics] core library .dll file copied to $$DEST_LIBDIRS)
        }

        exists($$GX_DLL_PHYSICS) {
            QMAKE_POST_LINK += $$escape_expand(\\n\\t)$(COPY_FILE) $$shell_quote($$GX_DLL_PHYSICS) $$shell_quote($$GX_DLL_PHYSICS_DEST)
            message([genesisx_physics] physics library .dll file copied to $$DEST_LIBDIRS)
        }
    }
}
